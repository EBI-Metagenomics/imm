add_custom_target(all_fuzzs)

function(imm_add_fuzz tgt seed max_len runs)
    add_executable(${tgt} ${tgt}.c)
    target_link_libraries(${tgt} PRIVATE IMM::imm)
    target_compile_options(${tgt} PRIVATE ${WARNING_FLAGS})
    target_compile_features(${tgt} PRIVATE c_std_11)

    add_executable(${seed} ${seed}.c)
    target_link_libraries(${seed} PRIVATE IMM::imm)
    target_compile_options(${seed} PRIVATE ${WARNING_FLAGS})
    target_compile_features(${seed} PRIVATE c_std_11)

    add_dependencies(all_fuzzs ${tgt} ${seed})

    set(CORPUS "${CMAKE_CURRENT_BINARY_DIR}/${tgt}.corpus")
    target_compile_definitions(${tgt} PUBLIC "CORPUS=\"${CORPUS}\"")
    target_compile_definitions(${seed} PUBLIC "CORPUS=\"${CORPUS}\"")

    set(DICT "${CMAKE_CURRENT_BINARY_DIR}/${tgt}.dict")
    configure_file(
        ${CMAKE_CURRENT_SOURCE_DIR}/${tgt}.dict
        ${DICT}
        COPYONLY
    )

    add_custom_command(
        TARGET ${seed} PRE_BUILD
        COMMAND ${CMAKE_COMMAND} -E make_directory ${CORPUS}
    )

    add_custom_command(
        TARGET ${tgt} PRE_BUILD
        COMMAND ${seed}
    )

    add_custom_command(
        TARGET ${tgt} POST_BUILD
        COMMAND ${tgt} ${CORPUS}
            -max_len=${max_len} -runs=${runs}
            -seed=1 -verbosity=0
            -dict=${DICT}
        VERBATIM
    )
endfunction()

imm_add_fuzz(
    abc_fuzz abc_seed 512 1000000
)
imm_add_fuzz(
    dp_fuzz dp_seed 33554432 10000
)
